plugins {
    id 'multiloader-loader'
    id 'fabric-loom' version '1.7-SNAPSHOT'
}

repositories {
    maven {
        name = "OnyxStudios/Ladysnake"
        url = 'https://maven.ladysnake.org/releases'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    // mappings loom.officialMojangMappings()
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.21:2024.07.28@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modImplementation "curse.maven:monolib-968432:5838449"

    // Cardinal Components (Required for Trinkets)
    modApi include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:6.1.1")
    modApi include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:6.1.1")

    // Trinkets (Curios (Fabric) was discontinued for 1.17+)
    modImplementation "curse.maven:trinkets-341284:5534317"
}

loom {
    def aw = project(":common").file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("runs/client")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("runs/server")
        }
    }
}
